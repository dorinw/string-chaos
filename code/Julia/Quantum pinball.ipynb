{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "e3235387-b154-4e2d-ab4b-418f10e8b49f",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Load all dependencies\n",
    "using Plots\n",
    "using SpecialFunctions, LinearAlgebra\n",
    "using LaTeXStrings\n",
    "using Dates\n",
    "using Memoize\n",
    "\n",
    "using Images\n",
    "\n",
    "using CSV\n",
    "\n",
    "using Statistics"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "13d94e78-f9df-4e87-9fbe-a279aba4222c",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Auxiliary functions\n",
    "\n",
    "# default figure style\n",
    "res = 640\n",
    "function newfig()\n",
    "    Figure(size = (res,res), fontsize = 24)\n",
    "end\n",
    "\n",
    "# file name for temporary figures\n",
    "function fname()\n",
    "    tt = replace(string(now()),\":\"=>\"-\")\n",
    "    tt = replace(tt,\".\"=>\"-\")\n",
    "    ext = \".png\"\n",
    "    path = \"c:\\\\Users\\\\Dorin\\\\Dropbox\\\\Julia\\\\figures\\\\\"\n",
    "    join([path,tt,ext])\n",
    "end\n",
    "\n",
    "# mimic Mathematica's chop function\n",
    "# adjust tolerance to adequate value to avoid evaluations on unnecessary tiny numbers\n",
    "function chop(x::Real; tol=1e-20)\n",
    "    abs(x) < tol ? zero(x) : x\n",
    "end\n",
    "\n",
    "function chop(z::Complex; tol=1e-20)\n",
    "    real_part = abs(real(z)) < tol ? 0.0 : real(z)\n",
    "    imag_part = abs(imag(z)) < tol ? 0.0 : imag(z)\n",
    "    complex(real_part, imag_part)\n",
    "end\n",
    "\n",
    "# A matrix of values V(ϕ,l) = exp(-i l (ϕ-π/2)), used for fast evaluation of the scattering amplitude\n",
    "function fmat(xs)\n",
    "    [exp(-im*l*(x-pi/2)) for x in xs, l in ls]\n",
    "end\n",
    "\n",
    "# A matrix of values V(ϕ,l) = exp(-i l (ϕ-π/2)), used for fast evaluation of the scattering amplitude\n",
    "function fmat(xs,lmax)\n",
    "    [exp(-im*l*(x-pi/2)) for x in xs, l in -lmax:lmax]\n",
    "end\n",
    "\n",
    "# The signed angle between two 2D vectors, used to evaluate αij\n",
    "function angbetween(v1,v2)\n",
    "    atan(v1[1]*v2[2]-v1[2]*v2[1],v1[1]*v2[1]+v1[2]*v2[2])\n",
    "end\n",
    "\n",
    "# Spacing ratios\n",
    "function rd(δ)\n",
    "    n = length(δ)\n",
    "    return δ[2:n]./δ[1:n-1]\n",
    "end\n",
    "\n",
    "# Definition of r-tilde = min(r,1/r)\n",
    "function rx(x)\n",
    "    return min(x,1/x)\n",
    "end\n",
    "\n",
    "# GOE distribution of spacings\n",
    "function pdfO(x)\n",
    "    pi/2 * x * exp(-pi/4*x^2)\n",
    "end\n",
    "\n",
    "# GOE distribution of r-tilde\n",
    "function pdfOR(r)\n",
    "    54/8 * (r+r^2) / (1+r+r^2)^(5/2)\n",
    "end\n",
    "\n",
    "\n"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "891aab25-2c9e-4e85-bc45-cb093c570119",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Shorthand for Bessel and Hankel functions, memoized to avoid repeated evaluation\n",
    "@memoize h1(n,x) = hankelh1(n,x)\n",
    "@memoize j(n,x) = besselj(n,x)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "fa6373c1-995a-4672-a00f-6b7d4300d430",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Define the system: 3 or 2 disk (equilateral triangle, changing sizes)\n",
    "\n",
    "# Placement of the disks\n",
    "L = 3.\n",
    "d1 = [L/sqrt(3), 0]\n",
    "d2 = [-L/sqrt(3)/2, L/2]\n",
    "d3 = [-L/sqrt(3)/2, -L/2]\n",
    "d0 = [0,0];\n",
    "\n",
    "da = [d1, d2, d3]\n",
    "\n",
    "# Sizes of the disks\n",
    "eps = 0.2\n",
    "R1 = 1.\n",
    "R2 = 1+eps\n",
    "R3 = 1-eps\n",
    "Ra = [R1, R2, R3]\n",
    "\n",
    "\n",
    "# Number of disks, set to 2 or 3\n",
    "# if nd = 2 keep only the disks R1 and R2\n",
    "nd = 3;"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "69d4acc0-a484-4ffb-aa98-44cc56e0b06c",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Define the system: 4-disks\n",
    "\n",
    "# Placement of the disks\n",
    "L = 4.\n",
    "d1 = [L/2,0]\n",
    "d2 = [-L/2,0]\n",
    "d3 = [0, L/2]\n",
    "d4 = [0,-L/2]\n",
    "d0 = [0,0];\n",
    "\n",
    "da = [d1, d2, d3, d4]\n",
    "\n",
    "# Sizes of the disks\n",
    "eps = 0.\n",
    "R1 = 1.2\n",
    "R2 = 1.1\n",
    "R3 = 1.0\n",
    "R4 = 0.9\n",
    "\n",
    "Ra = [R1, R2, R3, R4]\n",
    "\n",
    "\n",
    "# Set number of disks\n",
    "nd = 4;\n",
    "\n",
    "# In principle the code below will run for any nd,\n",
    "# but I've not implemented any way to define a system automatically,\n",
    "# so one should define the positions and sizes manually"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "51e150f9-e621-40ac-9181-67316bc0f060",
   "metadata": {},
   "outputs": [],
   "source": [
    "#Define all the variables that enter the formulas in the computation of the S-matrix\n",
    "# Don't forget to run after changing configuration!\n",
    "sa = [norm(da[a]-d0) for a in 1:nd]\n",
    "Lab = [norm(da[a]-da[b]) for a in 1:nd, b in 1:nd]\n",
    "chia = [atan(da[a][2],da[a][1]) for a in 1:nd]\n",
    "aab = [angbetween(da[a],da[b]-da[a])*(1-(a==b)) for a = 1:nd, b = 1:nd]"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "a2d22b6a-437e-44bd-939d-f7f8ae0534ab",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Components of the C, D, and M-matrices\n",
    "# Formulas from Wirzba 1997\n",
    "function Cl(k,l,a)\n",
    "    [2 * im / pi / Ra[a] * exp(im*l*chia[a]) * j(l-m,k*sa[a]) / h1(m,k*Ra[a]) for m in ms]\n",
    "end\n",
    "\n",
    "function Cl(k,l)\n",
    "    reduce(vcat,[Cl(k,l,a) for a in 1:nd])\n",
    "end\n",
    "\n",
    "function Dl(k,l,a)\n",
    "    [pi * Ra[a] * j(l-m,k*sa[a]) * j(m,k*Ra[a]) * exp(-im*l*chia[a]) for m in ms]\n",
    "end\n",
    "\n",
    "function Dl(k,l)\n",
    "    reduce(vcat,[Dl(k,l,a) for a in 1:nd])\n",
    "end\n",
    "\n",
    "function MM(k,a,b)\n",
    "    if a == b\n",
    "        return [ComplexF64(1)*(m1==m2) for m1 in ms, m2 in ms]\n",
    "    else\n",
    "        return [Ra[a] / Ra[b] * j(m1,k*Ra[a]) / h1(m2,k*Ra[b]) * h1(m1-m2,k*Lab[a,b]) * exp(im*(m1 * aab[a,b]-m2*(aab[b,a]-pi))) for m1 in ms, m2 in ms]\n",
    "    end\n",
    "end\n",
    "\n",
    "function MM(k)\n",
    "    res = Array{Matrix{ComplexF64}}(undef,nd^2)\n",
    "    for a = 1:nd\n",
    "        for b = 1:nd\n",
    "            res[nd*(a-1)+b] = MM(k,a,b)\n",
    "        end\n",
    "    end\n",
    "    hvcat(nd,res...)\n",
    "end    "
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "6832e861-c63a-409b-a557-6ad5ae4958b7",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Compute the M-matrix\n",
    "\n",
    "# Set value of k here\n",
    "k = 25\n",
    "\n",
    "# Cut-off size of the M-matrices:\n",
    "# We set it to an empirical value of 1.7k,\n",
    "# but this assumes that the lengths in the system are of order 1, adjust if necessary\n",
    "mu = max(round(1.7*k),20)\n",
    "ms = -mu:mu\n",
    "\n",
    "bigM = chop.(MM(k))\n",
    "iM = @time chop.(inv(bigM));\n",
    "\n",
    "# Draw heatmap of M to verify that elements outside the mu*mu blocks are zero,\n",
    "# as a basic consistency check.\n",
    "heatmap(abs.(bigM))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "aab4cfd7-53bc-4bc1-8745-f75a2c94ebd7",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Cut-off value for the S-matrix size: set empirically to nd*mu\n",
    "lmax = Int64(2*nd*mu)\n",
    "ls = -lmax:lmax\n",
    "\n",
    "# T = @time [transpose(Cl(k,l1)) * iM * Dl(k,l2) for l1 in ls, l2 in ls]\n",
    "is = Int32.(ls.+lmax.+1)\n",
    "@time Cls = [chop.(Cl(k,l1)) for l1 in ls]\n",
    "@time Dls = [chop.(Dl(k,l2)) for l2 in ls]\n",
    "\n",
    "T = Matrix{ComplexF64}(undef,2*lmax+1,2*lmax+1)\n",
    "@time for i in is\n",
    "    CiM = transpose(Cls[i]) * iM\n",
    "    for j in is\n",
    "        T[i,j] = CiM*Dls[j]\n",
    "    end\n",
    "end\n",
    "# T = @time [transpose(Cls[i]) * iM * Dls[j] for i in is, j in is]\n",
    "S = I + im * T;\n",
    "\n",
    "# Unitarity check, if failed, increase cut-offs and try again\n",
    "lambdas = eigvals(S)\n",
    "maxerr = maximum(abs.(abs.(eigvals(S)).-1))\n",
    "if maxerr >1e-9\n",
    "    println(\"UNITARITY CHECK FAILED!\")\n",
    "end\n",
    "println(maxerr)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "8905ae2d-a362-4934-87a5-a17ef89ffbc9",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Two dimensional plots of the cross-section\n",
    "\n",
    "# Number of points\n",
    "Np = 2 * res\n",
    "\n",
    "\n",
    "# Full range\n",
    "phi1_min = -pi\n",
    "phi1_max = pi\n",
    "phi2_min = -pi\n",
    "phi2_max = 2*pi\n",
    "\n",
    "# #Zoomed-in range\n",
    "# phi1_min = -pi+1\n",
    "# phi1_max = -pi+3\n",
    "# phi2_min = 0\n",
    "# phi2_max = 2\n",
    "# Np = 2 * res\n",
    "\n",
    "# # Zoomed-in range\n",
    "# phi1_min = -1.5\n",
    "# phi1_max = -1.3\n",
    "# phi2_min = 1.3\n",
    "# phi2_max = 1.4\n",
    "\n",
    "# Set aspect ratio to be 1 in the figures\n",
    "ratio = (phi2_max-phi2_min)/(phi1_max-phi1_min)\n",
    "\n",
    "phi1 = collect(range(phi1_min,phi1_max,Np))\n",
    "phi2 = collect(range(phi2_min,phi2_max,Int64(round(ratio*Np))))\n",
    "\n",
    "fl1 = fmat(phi1)\n",
    "fl2 = fmat(phi2)\n",
    "\n",
    "# The amplitude is evaluated by simple matrix multiplication:\n",
    "# f = f(ϕ1,l1) * T(l1,l2) * f†(l2,ϕ2)\n",
    "amplitude = @time fl1 * T * adjoint(fl2)\n",
    "\n",
    "# The differential cross section (with appropriate normalization is):\n",
    "dsigma = 1/2/pi/k*abs.(amplitude).^2"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "89cc402b-a5ab-44ef-abc9-78455949843a",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Plot of dσ\n",
    "\n",
    "# Set the maximum value to be plotted to see details of the plot\n",
    "# Otherwise the large values on the line ϕ1=ϕ2 will hide all other features\n",
    "threshold = 10\n",
    "\n",
    "# heatmap plot\n",
    "# Note: title is set manually for now, don't forget to change\n",
    "fig = @time heatmap(phi2,phi1,min.(dsigma,threshold),\n",
    "    color=reverse(cgrad(:Blues,12,categorical=true)),legend=false,\n",
    "    aspect_ratio=:equal,\n",
    "    size=(Int64(round(ratio*res)),res),\n",
    "    tickfontsize=16,titlefontsize=18,\n",
    "    title=\"k = 25 (four-disk)\",\n",
    "    interpolate=false\n",
    "    )\n",
    "xlims!(phi2_min,phi2_max)\n",
    "ylims!(phi1_min,phi1_max)\n",
    "@time savefig(fname())\n",
    "@time fig"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "64832969-c9ee-4876-bf7d-c13cfb83a1a0",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Find local maxima of dsigma\n",
    "# If resolution is high enough, should be close to the positions of the peaks\n",
    "\n",
    "ixs= @time Images.findlocalmaxima(dsigma)\n",
    "\n",
    "ps = []\n",
    "\n",
    "\n",
    "# Exclude points outside the chosen fundamental region (parity symmetry)\n",
    "for ix in Tuple.(ixs)\n",
    "    p = [phi1[ix[1]],phi2[ix[2]]]\n",
    "    if (p[1] < p[2] - 0.1 && p[1] >=  p[2] - pi + 0.1)\n",
    "        push!(ps,p)\n",
    "    end\n",
    "end\n",
    "\n",
    "# Display total number of points, total number of peaks, and number of peaks in the region\n",
    "[length(phi1)*length(phi2),length(ixs),length(ps)]"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "122bc091-6199-4cc2-a5a1-2e3599eb6b7c",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Find the local maxima on a discretized grid introduces some false positives\n",
    "# Eliminate points that are too near to each other by setting a threshold (manually at this stage)\n",
    "\n",
    "# Filter peaks: throw eigenvalues that are too near another one\n",
    "peaks = []\n",
    "c1 = []\n",
    "c2 = []\n",
    "threshold = 0.020\n",
    "push!(peaks,ps[1])\n",
    "for i in 2:length(ps)\n",
    "    p = ps[i]\n",
    "    (mm, mpos) = findmin([norm(p-peak) for peak in peaks])\n",
    "    if mm > threshold\n",
    "        push!(peaks,p)\n",
    "        push!(c1,p[1])\n",
    "        push!(c2,p[2])\n",
    "    end\n",
    "end\n",
    "\n",
    "δij = []\n",
    "for i in 1:length(ps)-1\n",
    "    for j in (i+1):length(peaks)\n",
    "        mm = norm(peaks[i]-peaks[j])\n",
    "        if mm < 0.05\n",
    "            push!(δij,norm(peaks[i]-peaks[j]))\n",
    "        end\n",
    "    end\n",
    "end\n",
    "\n",
    "# Check that histogram of spacings now doesn't have the maximum near zero\n",
    "# (which was always an artifact of the way peaks are found)\n",
    "histogram(δij)\n",
    "# sort(δij)[1:150]"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "b1421e1a-7794-4163-ae9c-77abd2b24f40",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Mar locations of peaks in plot of function\n",
    "plot(fig)\n",
    "scatter!(c2,c1,markersize=2.5,aspectratio=:equal,color=:yellow)\n",
    "plot!([-pi,pi,2*pi,0,-pi],[-pi,pi,pi,-pi,-pi],color=:red,linewidth=2)\n",
    "savefig(fname())"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "9e52e5d0-f860-4471-8ab3-7abe48427831",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Save locations of peaks to file\n",
    "CSV.write(\"lambda_k50_twodisk.csv\",(x = c1, y = c2))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "a00c4b77-428f-4637-8680-fe84b65258c0",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Plot distribution of spacings of eigenvalues of the S-matrix\n",
    "ϕn = sort(mod.(angle.(lambdas),2*pi))\n",
    "ϕn = filter(x -> (2*pi - x > 0.1) & (x > 0.1), ϕn)\n",
    "δn = diff(ϕn)\n",
    "δn = δn / mean(δn)\n",
    "\n",
    "histogram(δn, normalize=:pdf, bins=100, alpha=0.5,label=false)\n",
    "\n",
    "x = collect(range(0,3,1000))\n",
    "plot!(x,pdfO.(x),label=\"GOE\")"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "38888f78-ba6d-4463-9b69-571d0116a4df",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Plot distribution of spacing ratios of eigenvalues of the S-matrix\n",
    "rn = rx.(rd(δn))\n",
    "show([mean(rn),4-2*sqrt(3)])\n",
    "\n",
    "histogram(rn, normalize=:pdf, bins=40, alpha=0.5,aspectratio=1/4,label=false)\n",
    "xlims!(0,1)\n",
    "ylims!(0,2)\n",
    "\n",
    "x = collect(range(0,1,400))\n",
    "plot!(x,pdfOR.(x),label=\"GOE\")"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "242320dd-a97f-4cf8-ac56-2e326a0d875e",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Find a resonance by manual scan near a candidate point\n",
    "# I haven't implemented any automated way to scan for resonances\n",
    "\n",
    "# This is near a resonance for the R_i = (1,1.2,0.8), L = 3 system.\n",
    "x0 = 12.3364;\n",
    "y0 = -0.3269;\n",
    "\n",
    "# # \n",
    "# x0 = 10.52;\n",
    "# y0 = -0.3;\n",
    "\n",
    "kres = x0 + im * y0\n",
    "dx = 0.0001;\n",
    "dy = 0.0001;\n",
    "detM = @time [log(abs(det(MM(x0 + dx * x + im* (y0 - dy * y))))) for x in -3:3, y in -3:3]\n",
    "xy = @time [[x0+dx*x + im*(y0 - dy*y)] for x in -3:3, y in -3:3]\n",
    "\n",
    "# After computing detM, see where to move the point to get the minimum...\n",
    "detM"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "d52924aa-c1cb-46e6-8fec-b7be23c0783b",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Compute cross-section as a function of k\n",
    "ks = collect(5:0.05:30)\n",
    "\n",
    "f1 = []\n",
    "f2 = []\n",
    "f3 = []\n",
    "\n",
    "@time for k in ks\n",
    "    mu = max(round(1.7*k),20)\n",
    "    ms = -mu:mu\n",
    "\n",
    "    bigM = chop.(MM(k))\n",
    "    iM = chop.(inv(bigM));\n",
    "\n",
    "    # Cut-off value for the S-matrix size: set empirically to nd*mu\n",
    "    lmax = Int64(2*nd*mu)\n",
    "    ls = -lmax:lmax\n",
    "\n",
    "    \n",
    "    is = Int32.(ls.+lmax.+1)\n",
    "    Cls = [chop.(Cl(k,l1)) for l1 in ls]\n",
    "    Dls = [chop.(Dl(k,l2)) for l2 in ls]\n",
    "\n",
    "    T = Matrix{ComplexF64}(undef,2*lmax+1,2*lmax+1)\n",
    "    for i in is\n",
    "        CiM = transpose(Cls[i]) * iM\n",
    "        for j in is\n",
    "            T[i,j] = CiM*Dls[j]\n",
    "        end\n",
    "    end\n",
    "    TTd = T * adjoint(T)\n",
    "    \n",
    "    push!(f1,real(1/k * tr(TTd)))\n",
    "    push!(f2,real(1/k * transpose(fmat(0)) * TTd * conj(fmat(0))))\n",
    "    push!(f3,real(1/k * transpose(fmat(0.4)) * TTd * conj(fmat(0.4))))\n",
    "    print(\"*\")\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "ec85cd37-0db4-41f3-879c-9ed97d25b928",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Plot the results of above cell\n",
    "plot(ks,f1,\n",
    "    color=:black,linewidth=2,\n",
    "    size=(720,480),\n",
    "    tickfontsize=14,titlefontsize=16,legendfontsize=12,labelfontsize=16,\n",
    "    label=\"σ̄\",\n",
    "    title=\"Symmetric setup\",\n",
    "    xlabel = \"k\", ylabel = \"σ\"\n",
    ")\n",
    "plot!(ks,real.(f2),color=:blue,linestyle=:dash,label=\"σ(ϕ=0)\")\n",
    "plot!(ks,real.(f3),color=:red,linestyle=:dash,label=\"σ(ϕ=0.4)\")\n",
    "savefig(fname())"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "1f9e00f9-4eea-499a-85ed-c12fa5eaf5d2",
   "metadata": {},
   "outputs": [],
   "source": [
    "# One dimensional plots of the cross-section\n",
    "\n",
    "# Number of points\n",
    "Np = 10000\n",
    "\n",
    "\n",
    "# Full range\n",
    "phi2_min = -pi\n",
    "phi2_max = pi\n",
    "\n",
    "phi2 = collect(range(phi2_min,phi2_max,Np))\n",
    "\n",
    "fl1 = transpose(fmat(0))\n",
    "fl2 = fmat(phi2)\n",
    "\n",
    "# The amplitude is evaluated by simple matrix multiplication:\n",
    "# f = f(ϕ1,l1) * T(l1,l2) * f†(l2,ϕ2)\n",
    "amplitude = @time vec(fl1 * T * adjoint(fl2))\n",
    "\n",
    "# The differential cross section (with appropriate normalization is):\n",
    "dsigma = 1/2/pi/k*abs.(amplitude).^2\n",
    "\n",
    "peaks1 = []\n",
    "for i in 2:Np-1\n",
    "    if dsigma[i] > dsigma[i+1] && dsigma[i] > dsigma[i-1]\n",
    "        push!(peaks1,phi2[i])\n",
    "    end\n",
    "end\n",
    "\n",
    "plot(phi2,dsigma,linewidth=1,label=false,\n",
    "    size=(720,480),\n",
    "    title = \"Asymmetric cofiguration, ϕ=0\",\n",
    "    tickfontsize=14,titlefontsize=16,legendfontsize=12,labelfontsize=16,grid=false)\n",
    "vline!(peaks1,color=:grey,opacity=0.2,label=false)\n",
    "xlabel!(L\"\\phi\\prime\")\n",
    "ylabel!(L\"|f(k;\\phi,\\phi\\prime)|^2\")\n",
    "ylims!(0,7)\n",
    "xlims!(-pi,pi)\n",
    "savefig(fname())"
   ]
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.11.3",
   "language": "julia",
   "name": "julia-1.11"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.11.3"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
